(ns tasks
  (:require
   [clojure.string :as string]
   [babashka.fs :as fs]
   [dates :as dates]
   [tick.core :as t]
   ))

(defn log [& args]
  (apply println args))

;; vars/static data

(def repo-dir (str (fs/home) "/russmatney/blog"))
(def docs-dir (str repo-dir "/docs"))
(def posts-dir (str repo-dir "/docs/posts"))
(def hundos-dir (str repo-dir "/docs/posts/100-worders"))
(def org-garden-dir (str (fs/home) "/todo/garden"))
(def org-daily-dir (str (fs/home) "/todo/daily"))

(def generated-page-preamble
  "<!--- HELLO WORLD!!! \n  this page was GENERATED by some tasks.clj!\n  so-mind-ya-bizniz. --->\n\n")

(def side-bar-top-links
  "
* [Home](/home)
* [Dev Logs](/devlogs/)
* [Posts](/posts/)
"
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; data getters

;; posts

(defn parse-datey-fname [fname]
  (let [date-matches (re-seq #"(\d{4}-\d{2}-\d{2})-" fname)
        date         (some-> date-matches first last dates/parse-time-string)
        title        (cond-> fname
                       (some-> date-matches first last)
                       (string/replace (-> date-matches first first) "")

                       true
                       (string/replace ".md" ""))
        ]
    {:title title
     :date  date
     ;; :matches date-matches
     }))

(comment
  (parse-datey-fname "2022-04-22-bloodie-blah.md"))

(defn ->post
  [{:keys [garden-path]
    :as   post}]
  (let [path          (:post/path post garden-path)
        fname         (fs/file-name path)
        {title :title
         date  :date} (parse-datey-fname fname)]
    (cond-> post
      true (assoc
             :post/title title
             :post/fname fname
             :post/parent-fname (fs/file-name (fs/parent path))
             :post/last-modified (fs/file-time->millis (fs/last-modified-time path))
             ;; last time git-touched (all times git-touched?)
             ;; category
             )

      (:post/path post) (assoc :post/relative-url (string/replace path docs-dir ""))
      date              (assoc :post/date-created date))
    ))


(defn sort-latest-first [posts]
  (->> posts
       (sort-by (some-fn :post/date-created :post/last-modified) dates/sort-latest-first)))

(defn sort-oldest-first [posts]
  (->> posts
       (sort-by (some-fn :post/date-created :post/last-modified) dates/sort-chrono)))

;; post getters

(defn posts-in-dir [directory]
  (->> (fs/glob (str directory) "*.md")
       (map (fn [path] (->post {:post/path (str path)})))))

(defn local-posts []
  (->> (concat (fs/glob (str posts-dir) "*.md")
               (fs/glob (str posts-dir) "**/*.md"))
       (map (fn [path] (->post {:post/path (str path)})))
       ))

(defn hundos-posts []
  (posts-in-dir hundos-dir))

(comment
  (local-posts)
  (hundos-posts))

(defn garden-posts []
  (->> (fs/glob (str org-garden-dir) "*.org")
       (map (fn [path] (->post {:garden-path path})))))

(defn daily-posts []
  (->>
    (fs/glob (str org-daily-dir) "*.org")
    (map (fn [path] (->post {:garden-path path})))))

(comment
  (garden-posts)
  (daily-posts))

;; posts side bar


(defn write-sidebar [{directory :directory}]
  (let [path  (str directory "/_sidebar.md")
        posts (->> (posts-in-dir directory)
                   (remove (fn [{fname :post/fname}]
                             (#{"README.md" "_sidebar.md"} fname)))
                   sort-latest-first)
        post-links
        (->> posts
             (group-by (fn [post] (t/format "MMMM YYYY" (:post/date-created post))))
             (sort-by #(-> % second first :post/date-created) dates/sort-latest-first)
             (mapcat (fn [[group posts]]
                       [(str "\n\n"
                             group
                             "\n\n")
                        (->> posts
                             (map (fn [{:post/keys [relative-url title]}]
                                    (str "* [" title "](" relative-url ")")))
                             (string/join "\n"))])))]
    (log "Writing to" path)
    (spit path
          (str
            generated-page-preamble
            side-bar-top-links
            (string/join "\n" post-links)))))

(comment
  (write-sidebar {:directory hundos-dir}))

(defn write-index [{directory :directory}]
  (let [path  (str directory "/README.md")
        posts (->> (posts-in-dir directory)
                   (remove (fn [{fname :post/fname}]
                             (#{"README.md" "_sidebar.md"} fname)))
                   sort-oldest-first)
        post-links
        (->> posts
             (group-by (fn [post] (t/format "MMMM YYYY" (:post/date-created post))))
             (sort-by #(-> % second first :post/date-created) dates/sort-latest-first)
             (mapcat (fn [[group posts]]
                       [(str "\n\n"
                             group
                             "\n\n")
                        (->> posts
                             (map (fn [{:post/keys [relative-url title]}]
                                    (str "* [" title "](" relative-url ")")))
                             (string/join "\n"))])))]

    (log "Writing to" path)
    (spit path
          (str
            generated-page-preamble
            "## " (fs/file-name directory)
            (str "\n\n")
            (str "Known colloquially as hundos")
            (str "\n\n")
            (string/join "\n" (doall post-links))
            ))))

(comment
  (write-index {:directory hundos-dir}))

(defn regen-all-pages []
  (->> [hundos-dir]
       (map (fn [dir]
              (write-index {:directory dir})
              (write-sidebar {:directory dir})))
       doall))

(comment
  (regen-all-pages)
  (dates/now)
  )
